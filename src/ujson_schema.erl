-module(ujson_schema).
-author({ "David J Goehrig", "dave@dloh.org" }).
-copyright(<<"(C) 2016 David J. Goehrig"/utf8>>).

-export([ extract/2, format/1, data/1 ]).

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Public Methods
%%

%% data returns a packed data representation suitable for use with a schema
data(false) ->
	<< $f >>;
data(true) ->
	<< $t >>;
data(null) ->
	<< $n >>;
data(<<>>) ->
	<< 0:16/big-unsigned-integer >>;
data([]) ->
	<< 0:16/big-unsigned-integer >>;
data([{}]) ->
	<< 0:16/big-unsigned-integer >>;
data(Atom) when is_atom(Atom) ->
	data(list_to_binary(atom_to_list(Atom)));
data(String) when is_binary(String) ->	
	L = size(String),
	<< L:16/big-unsigned-integer, String/binary >>;
data(Int) when is_integer(Int), Int >= -128, Int < 128 ->
	<< Int:8/signed-integer >>;
data(Int) when is_integer(Int), Int >= 0, Int < 256 ->
	<< Int:8/unsigned-integer >>;
data(Int) when is_integer(Int), Int >= -32768, Int < 32768 ->
	<< Int:16/big-integer >>;
data(Int) when is_integer(Int), Int >= 0, Int < 65536 ->
	<< Int:16/big-unsigned-integer >>;
data(Int) when is_integer(Int), Int >= -2147483648, Int < 2147483648 ->
	<< Int:32/big-integer >>;
data(Int) when is_integer(Int), Int >= 0, Int < 4294967296 ->
	<< Int:32/big-unsigned-integer >>;
data(Int) when is_integer(Int), Int < 0 ->
	<< Int:64/big-integer >>;
data(Int) when is_integer(Int) ->
	<< Int:64/big-unsigned-integer >>;
data(Float) when is_float(Float), Float > 1.0e8 ; Float < -1.0e8 ->
	<< Float:64/big-float >>;
data(Float) when is_float(Float) ->
	<< Float:32/big-float >>;
data({K,V}) ->
	Len = size(K),
	Value = data(V),
	<< Len:16/big-unsigned-integer, K/binary, Value/binary >>;
data([H|T]) when is_tuple(H) ->
	data(object, [H|T], <<>>);
data([H|T]) ->
	data(array, [H|T], <<>>).

%% format returns a ujson schema object describing sample data
format(<<>>) ->
	null;
format(<< $n, Rest/binary>>) ->
	{ $n, Rest };
format(<< $t, Rest/binary>>) ->
	{ $b, Rest }; 
format(<< $f, Rest/binary>>) ->
	{ $b, Rest }; 
format(<< $c, _Int:8/signed, Rest/binary>>) ->
	{ $c, Rest };
format(<< $C, _Int:8/unsigned, Rest/binary>>) ->
	{ $C, Rest };
format(<< $w, _Int:16/big-integer, Rest/binary>>) ->
	{ $w, Rest };
format(<< $W, _Int:16/big-unsigned-integer, Rest/binary>>) ->
	{ $W, Rest };
format(<< $i, _Int:32/big-integer, Rest/binary>>) ->
	{ $i, Rest };
format(<< $I, _Int:32/big-unsigned-integer, Rest/binary>>) ->
	{ $I, Rest };
format(<< $q, _Int:64/big-integer, Rest/binary>>) ->
	{ $q, Rest };
format(<< $Q, _Int:64/big-unsigned-integer, Rest/binary>>) ->
	{ $Q, Rest };
format(<< $d, _Float:32/big-float, Rest/binary>>) ->
	{ $d, Rest }; 
format(<< $D, _Float:64/big-float, Rest/binary>>) ->
	{ $D, Rest }; 
format(<< $s, 0:16/big-unsigned-integer, Rest/binary>>) ->
	{ $s, Rest };
format(<< $s, Size:16/big-unsigned-integer, _String:Size/binary, Rest/binary >>) ->
	{ $s, Rest };
format(<< $a, 0:16/big-unsigned-integer, Rest/binary >>) ->
	{ $a, Rest };
format(<< $a, Size:16/big-unsigned-integer, Array:Size/binary, Rest/binary >>) ->
	{ format_array(Array,[]), Rest };
format(<< $o, 0:16/big-unsigned-integer, Rest/binary >>) ->
	{ $o, Rest };
format(<< $o, Size:16/big-unsigned-integer, Object:Size/binary, Rest/binary >>) ->
	{ format_object(Object,[]), Rest }.

%% extract applies a Schema generated by schema:format to data by schema:data 
extract(Data,Schema) ->
	case extract(Data,Schema,[]) of
	{ [ Result ], <<>>, [] }  -> Result;
	{ [ Result ], Rest, Fmt } -> { Result, Rest, Fmt }
	end.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%% Private Methods
%%

%% data a list
data(object,[],Acc) ->
	L = size(Acc),
	<< L:16/big-unsigned-integer, Acc/binary >>;
data(array,[],Acc) ->
	L = size(Acc),
	<< L:16/big-unsigned-integer, Acc/binary >>;
data(Type, [H|T],Acc) ->
	Term = data(H),
	data(Type, T, << Acc/binary, Term/binary >>).

%% parse an array
format_array(<<>>, Acc) ->
	[ $a | lists:reverse(Acc) ];
format_array(Array, Acc ) ->
	{ Value, Rem } = format(Array),
	format_array(Rem, [ Value | Acc ]).

%% parse and object
format_object(<<>>, Acc) ->
	[ $o | lists:reverse(Acc) ];
format_object(<<Len:16/big-unsigned-integer, _Key:Len/binary, Data/binary>>,Acc) ->
	{ Value, Rem } = format(Data),
	format_object(Rem, [ Value| Acc ]).

extract(Data,[],Acc) ->
	{ lists:reverse(Acc), Data, [] };
extract(<<>>,Schema,Acc) ->
	{ lists:reverse(Acc), <<>>, Schema };
extract(<<$n, Rest/binary>>, [ _Tag | T ], Acc ) ->
	extract(Rest,T, [ null | Acc ]);  
extract(<<$t, Rest/binary>>, [ $b | T ], Acc ) ->
	extract(Rest,T, [ true | Acc ]);  
extract(<<$f, Rest/binary>>, [ $b | T ], Acc ) ->
	extract(Rest,T, [ false | Acc ]);
extract(<<Int:8/signed-integer, Rest/binary>>, [ $c | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Int:8/unsigned-integer, Rest/binary>>, [ $C | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Int:16/big-integer, Rest/binary>>, [ $w | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Int:16/big-unsigned-integer, Rest/binary>>, [ $W | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Int:32/big-integer, Rest/binary>>, [ $i | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Int:32/big-unsigned-integer, Rest/binary>>, [ $I | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Int:64/big-integer, Rest/binary>>, [ $q | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Int:64/big-unsigned-integer, Rest/binary>>, [ $Q | T ], Acc ) ->
	extract(Rest, T, [ Int | Acc ]);
extract(<<Float:32/big-float, Rest/binary>>, [ $d | T ], Acc ) ->
	extract(Rest, T, [ Float | Acc ]);
extract(<<Float:64/big-float, Rest/binary>>, [ $D | T ], Acc ) ->
	extract(Rest, T, [ Float | Acc ]);
extract(<<Size:16/big-unsigned-integer,String:Size/binary,Rest/binary>>, [ $s | T ], Acc ) ->
	extract(Rest, T, [ String | Acc ]);
extract(<<Size:16/big-unsigned-integer,Array:Size/binary,Rest/binary>>, [ $a | T ], Acc ) ->
	{ Obj, Rem, Fmt } = extract(Array, T, [] ),
	extract( << Rem/binary, Rest/binary>>, Fmt,  [ Obj | Acc ]);
extract(<<Size:16/big-unsigned-integer,Array:Size/binary,Rest/binary>>, [ [$a|S] | T ], Acc ) ->
	extract(Rest, T, [ extract(Array,S,[]) | Acc ]);

extract(<<Size:16/big-unsigned-integer,Object:Size/binary,Rest/binary>>, [ $o | T ], Acc ) ->
	{ Obj, Rem, Fmt } = extract_object(Object, T, [] ),
	extract( << Rem/binary, Rest/binary>>, Fmt,  [ Obj | Acc ]);
extract(<<Size:16/big-unsigned-integer,Object:Size/binary,Rest/binary>>, [ [$o|S] | T ], Acc ) ->
	extract(Rest, T, [ extract_object(Object,S,[]) | Acc ]).

extract_object(<<>>, _, Acc ) ->
	{ lists:reverse(Acc), <<>>, [] };
extract_object(_, [], Acc ) ->
	{ lists:reverse(Acc), <<>>, [] };
extract_object(<<Len:16/big-unsigned-integer,Key:Len/binary,Value/binary>>, [ Tag | T ], Acc) ->
	{ [ V ], Rem, _ } = extract(Value, [ Tag ], []),
	extract_object(Rem, T, [ { Key, V } | Acc ]).
	
